# ----------------------------------------------------------------------------
#
# Copyright (c) 2019 - 2021 by the OpFlow developers
#
# This file is part of OpFlow.
#
# OpFlow is free software and is distributed under the MPL v2.0 license.
# The full text of the license can be found in the file LICENSE at the top
# level directory of OpFlow.
#
# ----------------------------------------------------------------------------
#
# Find OpFlow by config
#
# ----------------------------------------------------------------------------

set(OPFLOW_HYPRE_EXTERNAL @OPFLOW_HYPRE_EXTERNAL@)
set(OPFLOW_WITH_VTK @OPFLOW_WITH_VTK@)
set(OPFLOW_VTK_EXTERNAL @OPFLOW_VTK_EXTERNAL@)
set(OPFLOW_CONFIG_TARGETS_FILE @config_targets_file@)
include(CMakeFindDependencyMacro)

# Find fmt & spdlog
find_dependency(fmt PATHS @CMAKE_INSTALL_PREFIX@ NO_DEFAULT_PATH)
find_dependency(spdlog PATHS @CMAKE_INSTALL_PREFIX@ NO_DEFAULT_PATH)

# Find HYPRE
if (OPFLOW_HYPRE_EXTERNAL)
    find_dependency(HYPRE CONFIG PATHS @HYPRE_DIR@)
else ()
    find_dependency(HYPRE CONFIG
            PATHS @CMAKE_INSTALL_PREFIX@ NO_DEFAULT_PATH)
endif ()

# Find VTK
if (OPFLOW_WITH_VTK)
    if (OPFLOW_VTK_EXTERNAL)
        find_dependency(VTK CONFIG PATHS @VTK_DIR@)
    else ()
        find_dependency(VTK CONFIG
                PATHS @CMAKE_INSTALL_PREFIX@ NO_DEFAULT_PATH)
    endif ()
    target_include_directories(opflow INTERFACE ${VTK_INCLUDE_DIRS})
    target_link_directories(opflow INTERFACE ${VTK_LIBRARY_DIRS})
    target_link_libraries(opflow INTERFACE ${VTK_LIBRARIES})
endif ()

# Find MPI & OpenMP
set(OPFLOW_WITH_OPENMP @OPFLOW_WITH_OPENMP@)
if (${OPFLOW_WITH_OPENMP})
    find_dependency(OpenMP)
endif ()
set(OPFLOW_WITH_MPI @OPFLOW_WITH_MPI@)
if (${OPFLOW_WITH_MPI})
    find_dependency(MPI)
endif ()

# Find HDF5
set(OPFLOW_WITH_HDF5 @OPFLOW_WITH_HDF5@)
if (${OPFLOW_WITH_HDF5})
    if (OPFLOW_WITH_MPI)
        set(HDF5_PREFER_PARALLEL TRUE)
    endif ()
    set(HDF5_USE_STATIC_LIBRARIES ON)
    find_dependency(HDF5 COMPONENTS C REQUIRED QUIET)
    if (HDF5_PREFER_PARALLEL AND NOT HDF5_IS_PARALLEL)
        message(FATAL_ERROR "Cannot find parallel HDF5 library")
    endif ()
endif ()

# Find AMGCL
find_dependency(amgcl PATHS @CMAKE_INSTALL_PREFIX@ NO_DEFAULT_PATH)

# Find TBB
if (OPFLOW_TBB_EXTERNAL)
    find_dependency(TBB CONFIG REQUIRED PATHS @TBB_DIR@)
else()
    find_dependency(TBB CONFIG PATHS @CMAKE_INSTALL_PREFIX@ NO_DEFAULT_PATH)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/${OPFLOW_CONFIG_TARGETS_FILE}")
