# ----------------------------------------------------------------------------
#
# Copyright (c) 2019 - 2026 by the OpFlow developers
#
# This file is part of OpFlow.
#
# OpFlow is free software and is distributed under the MPL v2.0 license.
# The full text of the license can be found in the file LICENSE at the top
# level directory of OpFlow.
#
# ----------------------------------------------------------------------------
#
# Find OpFlow by config.
#
# OpFlow no longer vendors third-party dependencies. All dependencies are
# expected to be provided by the toolchain environment (e.g. conda).
#
# ----------------------------------------------------------------------------

include(CMakeFindDependencyMacro)

set(OPFLOW_WITH_OPENMP @OPFLOW_WITH_OPENMP@)
set(OPFLOW_WITH_MPI @OPFLOW_WITH_MPI@)

# Core dependencies
find_dependency(spdlog CONFIG REQUIRED)
find_dependency(TBB CONFIG REQUIRED)
find_dependency(amgcl CONFIG REQUIRED)
find_dependency(HYPRE CONFIG REQUIRED)

# Threading / MPI
if(OPFLOW_WITH_OPENMP)
    find_dependency(OpenMP REQUIRED)
endif()
if(OPFLOW_WITH_MPI)
    find_dependency(MPI REQUIRED)
endif()

# TecIO (serial or MPI)
if(OPFLOW_WITH_MPI)
    find_dependency(teciompi CONFIG REQUIRED)
else()
    find_dependency(tecio CONFIG REQUIRED)
endif()

# VTK is always required.
find_dependency(VTK CONFIG REQUIRED COMPONENTS CommonCore CommonDataModel IOXML)

# HDF5 is always required.
if(OPFLOW_WITH_MPI)
    set(HDF5_PREFER_PARALLEL TRUE)
endif()
# Prefer HDF5 from the same prefix as this package.
set(HDF5_ROOT "${PACKAGE_PREFIX_DIR}")
if(EXISTS "${PACKAGE_PREFIX_DIR}/bin/h5cc")
    set(HDF5_C_COMPILER_EXECUTABLE "${PACKAGE_PREFIX_DIR}/bin/h5cc")
endif()
find_dependency(HDF5 COMPONENTS C REQUIRED)
# NOTE:
# HDF5 CMake package metadata from conda-forge may report HDF5_IS_PARALLEL=FALSE
# even for mpi_* builds. We therefore enforce MPI HDF5 at package-resolution time
# (conda recipe variant constraints) rather than hard-failing here.

include("${CMAKE_CURRENT_LIST_DIR}/opflowTargets.cmake")
