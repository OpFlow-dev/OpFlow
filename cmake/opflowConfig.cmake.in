# ----------------------------------------------------------------------------
#
# Copyright (c) 2019 - 2021 by the OpFlow developers
#
# This file is part of OpFlow.
#
# OpFlow is free software and is distributed under the MPL v2.0 license.
# The full text of the license can be found in the file LICENSE at the top
# level directory of OpFlow.
#
# ----------------------------------------------------------------------------
#
# Find OpFlow by config
#
# ----------------------------------------------------------------------------

set(OPFLOW_FMT_EXTERNAL @OPFLOW_FMT_EXTERNAL@)
set(OPFLOW_SPDLOG_EXTERNAL @OPFLOW_SPDLOG_EXTERNAL@)
set(OPFLOW_HYPRE_EXTERNAL @OPFLOW_HYPRE_EXTERNAL@)
set(OPFLOW_VTK_EXTERNAL @OPFLOW_VTK_EXTERNAL@)
set(config_targets_file @config_targets_file@)
include(CMakeFindDependencyMacro)

if (OPFLOW_FMT_EXTERNAL)
    find_dependency(fmt CONFIG)
endif ()
if (OPFLOW_SPDLOG_EXTERNAL)
    find_dependency(spdlog CONFIG)
endif ()
if (OPFLOW_HYPRE_EXTERNAL)
    find_dependency(HYPRE CONFIG)
else ()
    find_dependency(HYPRE CONFIG
            PATHS @CMAKE_INSTALL_PREFIX@)
endif ()
if (OPFLOW_VTK_EXTERNAL)
    find_dependency(VTK CONFIG)
else ()
    find_dependency(VTK CONFIG
            PATHS @CMAKE_INSTALL_PREFIX@)
endif ()

# Find MPI & OpenMP
set(OPFLOW_WITH_OPENMP @OPFLOW_WITH_OPENMP@)
if (${OPFLOW_WITH_OPENMP})
    find_dependency(OpenMP)
endif ()
set(OPFLOW_WITH_MPI @OPFLOW_WITH_MPI@)
if (${OPFLOW_WITH_MPI})
    find_dependency(MPI)
endif ()

include("${CMAKE_CURRENT_LIST_DIR}/${config_targets_file}")
